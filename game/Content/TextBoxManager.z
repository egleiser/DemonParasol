/////////////////////////////////////////
////                                 ////
////  Name: TextBoxManager.z         ////
////    Written by: Jason Clark      ////
////                                 ////
////     Last Updated: 12-11-14      ////
////                                 ////
/////////////////////////////////////////
class TextBoxManager : ZilchComponent
{
    [Property]
    var TextBoxTextPath : CogPath = null;
    var TextBoxText : Cog = null;
    
    [Property]
    var ContinueButtonPath : CogPath = null;
    var ContinueButton : Cog = null;
    
    //Gamepad variables
    [Property]
    var GamepadIndex:Integer = 0;
    var Gamepad:Gamepad = null;
    
    //Delay between the text box appearing/changing and start of text scroll
    [Property]
    var SpeakerChangeDelay : Real = 0.1;
    
    //Dialogue Segment to get children TextBlockInfos from
    var DialogueSegment : Cog = null;
    //Index of the array craeted from the children
    var CurrentTextIdx : Integer = 0;
    //A new array of cogs to copy the children of Dialogue Segment into
    var ChildTexts : Array[Cog] = null;
    
    var DialogueFinished : Boolean = false;
    
    var DialogueStarted : Boolean = false;
    
    //The sprite source for the text box (each character has their own)
    var TextBoxSprite : SpriteSource = null;
    
    //The current sprite source loaded
    var CurrentSprite : SpriteSource = null;
    var SpriteToLoad : SpriteSource = null;
    
    var MainSpace : Space = null;
    
    function Initialize(init : CogInitializer)
    {
        this.Gamepad = Zero.Gamepads.GetGamePad(this.GamepadIndex);
        
        this.ChildTexts = new Array[Cog]();
        
        this.MainSpace = this.GameSession.FindSpaceByName("Main");
        
        this.TextBoxText = this.TextBoxTextPath.Cog;
        this.ContinueButton = this.ContinueButtonPath.Cog;
        
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, "startDialogue", this.OnStartDialogueEvent);
        Zero.Connect(this.Owner, "continueDialogue", this.OnContinueDialogueEvent);
        Zero.Connect(this.Owner, "CancelDialogue", this.OnCancelDialogue);
    }
    
    function OnLogicUpdate(event : UpdateEvent)
    {
        if(Zero.Keyboard.KeyIsPressed(Keys.N) || this.Gamepad.IsButtonPressed(Buttons.B))
        {
            this.CancelDialogue();
        }
    }

    function OnStartDialogueEvent(event : TextBoxEvent)
    {
        this.DialogueStarted = true;
        this.TextBoxText.TextScrollManager.DialogueSequence = true;
        //this.ClearDialogue();
        var mainSpace = this.GameSession.FindSpaceByName("Main");
            
        this.DialogueSegment = event.DialogueSegmentToUse;
        this.CurrentTextIdx = 0;
        
        if(this.DialogueSegment != null)
        {
            this.ChildTexts.Clear();
            
            foreach(var child in this.DialogueSegment.Children)
            {
                this.ChildTexts.Push(child);
            }
        }
        
        this.TextBoxSprite = this.ChildTexts.Get(0).TextBlockInfo.SpeakerSprite;
        this.Owner.Sprite.SpriteSource = this.TextBoxSprite;
        
        var seq = Action.Sequence(this.Owner.Actions);
        var grp = Action.Group(seq);
        //Action.Call(seq, this.FirstTextScroll);
        Action.Property(grp,
                        @this.Owner.Transform.Translation,
                        Real3(this.Owner.Transform.Translation.X, event.DistanceOnScreen, this.Owner.Transform.Translation.Z),
                        event.InOutTime,
                        event.EaseCurve);
        Action.Property(grp,
                        @this.Owner.Sprite.Color,
                        Real4(this.Owner.Sprite.Color.X, this.Owner.Sprite.Color.Y, this.Owner.Sprite.Color.Z, event.Opacity),
                        event.OpacityTimeIn,
                        event.OpacityEase);
        Action.Call(seq, this.FirstTextScroll);
    }
    
    function OnContinueDialogueEvent(event : ContinueDialogueEvent)
    {
        this.CurrentTextIdx += 1;
        
        if(this.CurrentTextIdx == this.ChildTexts.Count)
        {
            var seq = Action.Sequence(this.Owner.Actions);
            var grp = Action.Group(seq);
            this.TextBoxText.TextScrollManager.DialogueSequence = false;
            this.DialogueFinished = true;
            Action.Property(grp,
                            @this.Owner.Transform.Translation,
                            Real3(this.Owner.Transform.Translation.X, -13.0, this.Owner.Transform.Translation.Z),
                            0.3,
                            Ease.QuadIn);
                            
            Action.Property(grp,
                            @this.Owner.Sprite.Color,
                            Real4(this.Owner.Sprite.Color.X, this.Owner.Sprite.Color.Y, this.Owner.Sprite.Color.Z, 0),
                            0.3,
                            Ease.Linear);
            Action.Call(seq, this.TextBoxText.TextScrollManager.DialogueEndSettings);
        }
        else
        {
            var speakerSprite = this.ChildTexts.Get(this.CurrentTextIdx).TextBlockInfo.SpeakerSprite;
            Console.WriteLine(this.TextBoxSprite);
            if(this.TextBoxSprite != speakerSprite)
            {
                //Fades out current speaker's text box sprite
                var seq = Action.Sequence(this.Owner.Actions);
                var grp = Action.Group(seq);
                Action.Property(grp,
                                @this.Owner.Sprite.Color,
                                Real4(this.Owner.Sprite.Color.X, this.Owner.Sprite.Color.Y, this.Owner.Sprite.Color.Z, 0.0),
                                0.3,
                                Ease.QuadIn);
                Action.Property(grp,
                                @this.ContinueButton.Sprite.Color,
                                Real4(this.ContinueButton.Sprite.Color.X, this.ContinueButton.Sprite.Color.Y, this.ContinueButton.Sprite.Color.Z, 0.0),
                                0.3,
                                Ease.QuadIn);
                                
                Action.Call(seq, this.ContinueScrollEventCall);
                //Call to function changing the text box sprite source to current speaker
                Action.Call(seq, this.SwitchSprite);
                
                var grp2 = Action.Group(seq);
                //Fades in new speaker's sprite
                Action.Call(grp2, this.SwitchSprite);
                Action.Property(grp2,
                                @this.Owner.Sprite.Color,
                                Real4(this.Owner.Sprite.Color.X, this.Owner.Sprite.Color.Y, this.Owner.Sprite.Color.Z, 0.85),
                                0.3,
                                Ease.QuadOut);
                Action.Property(grp2,
                                @this.ContinueButton.Sprite.Color,
                                Real4(this.ContinueButton.Sprite.Color.X, this.ContinueButton.Sprite.Color.Y, this.ContinueButton.Sprite.Color.Z, 0.9),
                                0.3,
                                Ease.QuadOut);
                                
                //Call for continuing to next text scroll event
                //Action.Call(grp2, this.ContinueScrollEventCall);
            }
            else
            {
                if(this.CurrentTextIdx != this.ChildTexts.Count)
                {
                    var e = new TextScrollEvent();
                    e.TextToWrite = this.ChildTexts.Get(this.CurrentTextIdx).TextBlockInfo.SpeakerTextBlock;
                    this.TextBoxText.DispatchEvent("textScrollEvent", e);
                }
            }
        }
    }
    
    function SwitchSprite()
    {
        this.TextBoxSprite = this.ChildTexts.Get(this.CurrentTextIdx).TextBlockInfo.SpeakerSprite;
        this.Owner.Sprite.SpriteSource = this.TextBoxSprite;
    }
    
    function ContinueScrollEventCall()
    {
        var e = new TextScrollEvent();
        e.TextToWrite = this.ChildTexts.Get(this.CurrentTextIdx).TextBlockInfo.SpeakerTextBlock;
        this.TextBoxText.DispatchEvent("textScrollEvent", e);
    }
    
    function CancelDialogue()
    {
        Console.WriteLine("Dialogue Should Be Clear");
        this.TextBoxText.SpriteText.Text = "";
        var e = new CancelDialogueEvent();
        this.Owner.DispatchEvent("CancelDialogue", e);
        this.Space.DispatchEvent("CancelDialogue", e);
    }
    
    function OnCancelDialogue(event : CancelDialogueEvent)
    {
        this.DialogueFinished = true;
        var grp = Action.Group(this.Owner.Actions);
        Action.Property(grp,
                            @this.Owner.Transform.Translation,
                            Real3(this.Owner.Transform.Translation.X, -13.0, this.Owner.Transform.Translation.Z),
                            0.3,
                            Ease.QuadIn);
                            
        Action.Property(grp,
                        @this.Owner.Sprite.Color,
                        Real4(this.Owner.Sprite.Color.X, this.Owner.Sprite.Color.Y, this.Owner.Sprite.Color.Z, 0),
                        0.3,
                        Ease.Linear);
        Action.Call(grp, this.TextBoxText.TextScrollManager.DialogueEndSettings);
                        
        this.CurrentTextIdx = this.ChildTexts.Count;
    }
    
    function FirstTextScroll()
    {
        this.DialogueStarted = true;
        //Sending a new text scroll event to the Text Box Text (not text box)
        var e = new TextScrollEvent();
        e.TextToWrite = this.ChildTexts.Get(0).TextBlockInfo.SpeakerTextBlock;
        this.TextBoxText.DispatchEvent("textScrollEvent", e);
    }
    

}

