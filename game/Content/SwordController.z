/////////////////////////////////////////
////                                 ////
////  Name: SwordController.z        ////
////    Written by: Eric Gleiser     ////
////                                 ////
////     Last Updated: 12-11-14      ////
////                                 ////
/////////////////////////////////////////
class SwordController : ZilchComponent
{
    var SlashCount:Integer = 0;
    var Timer:Integer = 0;
    [Property]
    var GruntSound : SoundCue = null;
    [Property]
    var SwishSound : SoundCue = null;
    var Ribs : Cog = null;
    function Initialize(init : CogInitializer)
    {
        this.Ribs = this.Owner.FindChildByName("Ribs");
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, "attackEvent", this.OnAttack);
        Zero.Connect(this.Owner.FindChildByName("Blade"), Events.CollisionStarted, this.OnCollisionStarted);
        
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        this.Timer += 1;
        if(this.Timer >= 30)
        {
            this.SlashCount = 0;
        }
        if(this.SlashCount == 0 && this.Ribs.RibController.Open == false)
        {
            this.Owner.SimpleAnimation.CrossBlend(Animation.Find("IdleSlash"),0.5, AnimPlayMode.PlayOnce);
        }
        if(this.SlashCount == 0)
        {
            this.Owner.BoxCollider.Ghost = true;
            this.Owner.FindChildByName("Blade").BoxCollider.Ghost = true;
            this.Owner.FindChildByName("Blade").Ribbon.Spawning = false;
        }
        else
        {
            this.Owner.BoxCollider.Ghost = false;
            this.Owner.FindChildByName("Blade").BoxCollider.Ghost = false;
            this.Owner.FindChildByName("Blade").Ribbon.Spawning = true;
        }
        if(this.Ribs.RibController.Open)
        {
            this.Owner.SimpleAnimation.CrossBlend(Animation.Find("Blocking"),0.5, AnimPlayMode.PlayOnce);
        }
    }
    function OnAttack(event : AttackEvent)
    {
        if(this.Ribs.RibController.Open == false)
        {
            Console.WriteLine("Attack!");
            
            //Play Swish Sound
            this.Owner.SoundEmitter.PlayCue(this.SwishSound);
            
            this.SlashCount += 1;
            this.Timer = 2;
            if(this.SlashCount == 1)
            {
                this.Owner.SimpleAnimation.PlaySingle(Animation.Find("HorizontalSlash1"),AnimPlayMode.PlayOnce);
            }
            else if(this.SlashCount == 2)
            {
                this.Owner.SimpleAnimation.PlaySingle(Animation.Find("HorizontalSlash2"),AnimPlayMode.PlayOnce);
            }
            else if(this.SlashCount == 3)
            {
                this.Owner.SimpleAnimation.PlaySingle(Animation.Find("VerticalSlash1"),AnimPlayMode.PlayOnce);
                //Play Player Grunt
                this.Space.SoundSpace.PlayCueAt(this.GruntSound, this.Owner.Transform.Translation);
            }
            else
            {
                this.Owner.SimpleAnimation.PlaySingle(Animation.Find("HorizontalSlash1"),AnimPlayMode.PlayOnce);
                this.SlashCount = 0;
            }
        }
    }
    function OnCollisionStarted(event : CollisionEvent)
    {
         // Get the object we collided with:
        var otherObject = event.OtherObject;
        if(this.Owner.FindChildByName("Blade").BoxCollider.Ghost == false)
        {
            var hitEvent = new HitEvent();
            otherObject.DispatchDown("hitEvent", hitEvent);
            otherObject.DispatchEvent("hitEvent", hitEvent);
            if(otherObject.Collider.CollisionGroup.Name != "CG_Tongue")
            {
                this.Space.CreateAtPosition(Archetype.Find("HitSparks"), event.FirstPoint.WorldPoint);
            }
            
        }
    }
}
