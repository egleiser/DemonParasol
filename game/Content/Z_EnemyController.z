/*//////////////////////////////////////////////////////////////////////////////////////////////////////////
//SCRIPT-Z_EnemyController.z
//AUTHOR-Travis Moore
//COURSE-GAM205
//COPYRIGHT-© 2014 DigiPen, All Rights Reserved.
//////////////////////////////////////////////////////////////////////////////////////////////////////////*/

class Z_EnemyController:ZilchComponent
{
    //DEPENDENCIES
    [Dependency]
    var Orientation : Orientation = null;
    
    //ATTACK PROPERTIES
    [Property]
    var TargetObjectPath:CogPath = null;
    [Property]
    var TargetDetectionRange:Real = 30.0;
    [Property]
    var MaxAttackRange:Real = 15.0;
    [Property]
    var MaxAttacks:Integer = 1;
    [Property]
    var AttackTimeout:Real = 300.0; //miliseconds
    //non-settable properties
    var TargetObject:Cog = null;
    var TargetDirection:Real3 = Real3();
    var TargetDistance:Real = Real();
    var OriginalPosition:Real3 = Real3();
    var MushroomRotation:Quaternion = Quaternion();
    var AttackTimer:Real = 0.0;
    
    //LUNGE PROPERTIES
    [Property]
    var LungeSpeed:Real = 10.0;
    //non-settable properties
    var LungeDistance:Real = Real();
    
    //HEADBUTT PROPERTIES
    [Property]
    var HeadbuttAttackPercentage:Real = 0.5;
    [Property]
    var JumpHeight:Real = 1.0;
    [Property]
    var JumpSpeed:Real = 10.0;
    [Property]
    var FallingHeadbuttSpeed:Real = 30.0;
    //non-settable properies
    var UpsideDownRotation:Quaternion = Quaternion();
    
    /*//////////////////////////////////////////////////////////////////////////////////////////////////////
    //FUNCTION-Initialize
    //EXPLANATION-Initialize variables/functions so that we can use them
    //////////////////////////////////////////////////////////////////////////////////////////////////////*/
    function Initialize(init:CogInitializer)
    {
        //set the target of the enemy
        this.TargetObject = this.TargetObjectPath.Cog;
        
        //set the OriginalPosition of this object
        this.OriginalPosition = this.Owner.Transform.Translation;
        
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, Events.CollisionStarted, this.OnCollisionStarted);
    }
    /*//////////////////////////////////////////////////////////////////////////////////////////////////////
    //FUNCTION-OnCollisionStarted
    //EXPLANATION-Initialize variables/functions so that we can use them
    //////////////////////////////////////////////////////////////////////////////////////////////////////*/
    function OnCollisionStarted(event:CollisionEvent)
    {
        /*THIS DOES NOT WORK PROPERLY*********************************************************************************/
        //Console.WriteLine(event.OtherObject.Parent.Parent);
        if(event.OtherObject.Parent == this.TargetObject)
        {
            Console.WriteLine("boo");
        }
    }
    
    /*//////////////////////////////////////////////////////////////////////////////////////////////////////
    //FUNCTION-OnLogicUpdate
    //EXPLANATION-Update every frame, perform actions of the enemy
    //////////////////////////////////////////////////////////////////////////////////////////////////////*/
    function OnLogicUpdate(event:UpdateEvent):Void
    {
        //do we have a target?
        if(this.TargetObject != null)
        {
            //call function to get distance/direction
            this.GetDistanceAndDirectionToTarget();
            
            //enemy in detection range?
            if(this.TargetDistance <= this.TargetDetectionRange)
            {
                //rotate the enemy
                this.RotateTowardsTarget(event);
                //DEBUG
                //Console.WriteLine("`this.TargetObject` in range");
                
                //can we attack yet?
                if(this.AttackTimer >= this.AttackTimeout)
                {
                    //DEBUG
                    //Console.WriteLine("I can attack!");
                    
                    //enemy at lunge range?
                    if(this.TargetDistance <= this.MaxAttackRange)
                    {
                        //choose an attack
                        this.ChooseAttackType(event);
                        
                        //DEBUG
                        //Console.WriteLine("Attacking!");
                        
                        //reset AttackTimer
                        this.AttackTimer = 0.0;
                    }
                }
                
            }
            //increment AttackTimer
            ++this.AttackTimer;
            //DEBUG FOR LOCATION IN WORLD SPACE
            //Console.WriteLine("Where we are: `this.Owner.Transform.Translation`");
        }
    }
    
    /*//////////////////////////////////////////////////////////////////////////////////////////////////////
    //FUNCTION-ChooseAttackType
    //EXPLANATION-Chooses between lunging attacks and headbutt attacks
    //////////////////////////////////////////////////////////////////////////////////////////////////////*/
    function ChooseAttackType(event:UpdateEvent):Void
    {
        //pick a number between 0.0 and 1.0
        var random:Real = Random().Range(0.0, 1.0);
        
        //if random is less than or equal to the percentage chance of the HeadbuttAttackPercentage
        if(random <= this.HeadbuttAttackPercentage)
        {
            //DEBUG
            //Console.WriteLine("Headbutt!");
            this.Headbutt(event);
        }
        
        //the rest of the attacks are normal lunges
        else
        {
            //DEBUG
            //Console.WriteLine("Lunging!");
            this.Lunge(event);
        }
    }
    
    /*//////////////////////////////////////////////////////////////////////////////////////////////////////
    //FUNCTION-Lunge
    //EXPLANATION-applies a short term velocity on the enemy towards the target
    //////////////////////////////////////////////////////////////////////////////////////////////////////*/
    function Lunge(event:UpdateEvent):Void
    {
        //apply velocity on enemy towards target
        this.Owner.RigidBody.Velocity = -this.TargetDirection * this.LungeSpeed;
        
        //var lungeDisplacement:Real3 = this.Owner.Transform.Translation + this.OriginalPosition;
        //this.LungeDistance = Math.Length(lungeDisplacement);
    }
    
    /*//////////////////////////////////////////////////////////////////////////////////////////////////////
    //FUNCTION-Headbutt
    //EXPLANATION-applies a jump on the enemy, rotates in air, falls down on target
    //////////////////////////////////////////////////////////////////////////////////////////////////////*/
    function Headbutt(event:UpdateEvent):Void
    {
        //store rotation
        this.UpsideDownRotation = this.Owner.Orientation.GetLookAtDirectionWithUpRotation(this.TargetDirection, Real3(0,0,1));
        
        //create an action sequence for headbutting
        var aHeadbuttSequence = Action.Sequence(this.Owner.Actions);
        
        //apply velocity on enemy towards target
        Action.Property(aHeadbuttSequence,
                        @this.Owner.RigidBody.Velocity,
                        (-this.TargetDirection + Real3(0,0,this.JumpHeight)) * this.JumpSpeed,
                        0.25,
                        Ease.QuadOut);
        
        //rotate upside-down?
        Action.Property(aHeadbuttSequence,
                        @this.Owner.Transform.Rotation,
                        Math.Slerp(this.Owner.Transform.Rotation, this.MushroomRotation, 180),
                        1.0,
                        Ease.QuadOut);
        
        //crash down!
        Action.Property(aHeadbuttSequence,
                        @this.Owner.RigidBody.Velocity,
                        (Real3(0,0,-this.JumpHeight) * this.FallingHeadbuttSpeed),
                        0.25,
                        Ease.QuadOut);
                        
        //particles
    }
    
    /*//////////////////////////////////////////////////////////////////////////////////////////////////////
    //FUNCTION-DistanceToTarget
    //EXPLANATION-Calculates the distance/direction between this enemy and the target object
    //////////////////////////////////////////////////////////////////////////////////////////////////////*/
    function GetDistanceAndDirectionToTarget():Void
    {
        var playerPos = this.TargetObject.Transform.Translation;
        var enemyPos = this.Owner.Transform.Translation;
        
        //enemy - player
        this.TargetDirection = enemyPos - playerPos;
        //length
        this.TargetDistance = Math.Length(this.TargetDirection);
        //normalize
        this.TargetDirection = Math.Normalize(this.TargetDirection);
    }
    
    /*//////////////////////////////////////////////////////////////////////////////////////////////////////
    //FUNCTION-RotateTowardsTarget
    //EXPLANATION-Calculates the distance/direction between this enemy and the target object
    //////////////////////////////////////////////////////////////////////////////////////////////////////*/
    function RotateTowardsTarget(event:UpdateEvent):Void
    {
        //store rotation
        this.MushroomRotation = this.Owner.Orientation.GetLookAtDirectionWithUpRotation(this.TargetDirection, Real3(0,0,1));
        //perform rotation through slerping
        this.Owner.Transform.Rotation = Math.Slerp(this.Owner.Transform.Rotation, this.MushroomRotation, 0.1);
    }
}
