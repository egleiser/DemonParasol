//Written by: Jason Clark
//© 2015 DigiPen, All Rights Reserved.
class BunkerStart : ZilchComponent
{
    var BunkerSprite : Cog = null;
    var BunkerLine : Cog = null;
    var BargainSprite : Cog = null;
    var Hud : Space = null;
    var Camera : Cog = null;
    var FadeSprite : Cog = null;
    
    function Initialize(init : CogInitializer)
    {
        this.Hud = this.LevelSettings.HudCreator.HudSpace;
        this.Camera = this.Hud.FindObjectByName("Camera");
        this.FadeSprite = this.Camera.FindChildByName("FadeToBlackSprite");
        this.BunkerSprite = this.Camera.FindChildByName("BlackBunker");
        this.BunkerLine = this.BunkerSprite.FindChildByName("BlackBunkerLine");
        this.BargainSprite = this.Camera.FindChildByName("BargainText");
        
        //Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        if(!this.LevelSettings.DataLoader.SaveFile.SaveData.BlackRain)
        {
            this.Start();
        }
    }

    function Start()
    {
        this.FadeSprite.Sprite.Visible = true;
        
        var seq = Action.Sequence(this.Owner.Actions);
        
        Action.Call(seq, this.FadingIn);
        Action.Delay(seq, 1.5);
        
        var grp = Action.Group(seq);
        Action.Property(grp, @this.BunkerSprite.SpriteText.Color, Real4(1,1,1,1), 1.25, Ease.QuadInOut);
        Action.Property(grp, @this.BunkerLine.Sprite.Color, Real4(1,1,1,1), 1.25, Ease.QuadInOut);
        
        Action.Delay(seq, 0.5);
        Action.Property(seq, @this.BargainSprite.SpriteText.Color, Real4(1,1,1,1), 1.25, Ease.QuadInOut);
        Action.Delay(seq, 1.0);
        
        var grp2 = Action.Group(seq);
        Action.Property(grp2, @this.BunkerSprite.SpriteText.Color, Real4(1,1,1,0), 2.0, Ease.QuadInOut);
        Action.Property(grp2, @this.BunkerLine.Sprite.Color, Real4(1,1,1,0), 2.0, Ease.QuadInOut);
        Action.Property(grp2, @this.BargainSprite.SpriteText.Color, Real4(1,1,1,0), 2.0, Ease.QuadInOut);
        
        Action.Call(seq, this.UnfreezePlayer);
        this.LevelSettings.DataLoader.SaveFile.SaveData.BlackRain = true;
        this.LevelSettings.DataLoader.StoreSaveFile();
    }
    
    function FreezePlayer()
    {
        this.Space.FindObjectByName("Player").PlayerController.FreezeMovement = true;
    }
    
    function UnfreezePlayer()
    {
        this.Space.FindObjectByName("Player").PlayerController.FreezeMovement = false;
        this.FadeSprite.Sprite.Visible = false;
    }
    
    function FadingIn()
    {
        var startFadeIn = new FadeToBlackEvent();
        startFadeIn.IsFadeIn = true;
        startFadeIn.TimeToFade = 1.0;
        this.Camera.DispatchDown("startFadeIn", startFadeIn);
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
    }
}
