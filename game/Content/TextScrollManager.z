/////////////////////////////////////////
////                                 ////
////  Name: TextScrollManager.z      ////
////    Written by: Jason Clark      ////
////     Last Updated: 04-14-15      ////
////                                 ////
/////////////////////////////////////////
class TextScrollManager : ZilchComponent
{
    //TextBlock taken from TextBlockInfo
    var TextBlockToPrint : TextBlock = null;
    
    var DialogueSequence : Boolean = false;
    
    //The complete string that will be printed
    var GoalString : String = null;
    //The string as it exists each moment while it is printed
    var CurrentString : String = null;
    //The index of the character in the string that is being printed
    var CurrentIndex : Integer = 0;
    
    //Variables taken from the TextBlockElieInfo
    var SpeakerPicture : SpriteSource = null;
    var SpeakerName : String = "";
    
    [Property]
    var TextBoxPath : CogPath = null;
    var TextBox : Cog = null;
    
    //Gamepad variables
    [Property]
    var GamepadIndex:Integer = 0;
    var Gamepad:Gamepad = null;
    
    var PlayerDeactivated : Boolean = false;
    
    var TextDelay : Real = 0.0;
    var IsPunctuation : Boolean = false;
    
    //A higher delay to use after certain punctuation marks
    [Property]
    var PunctuationDelay : Real = 0.1;
    
    //The standard delay for printing characters
    [Property]
    var LetterDelay : Real = 0.05;
    
    var Particle : Cog  = null;
    var ParticleChild : Cog = null;
    //Boolean to check whether text is currently scrolling
    //Used if player wishes to have text scroll very quickly
    var TextIsScrolling : Boolean = false;
    
    var TextIsCancelled : Boolean = false;
    
    var DialogueSpeedIncreased : Boolean = false;
    
    var ButtonAPresses : Integer = 0;
    
    //The Action Set that controls the delays
    var TextSeq : ActionSet = null;
    //The Action Set to control delay between speaker changes
    var ChangeSeq : ActionSet = null;
    
    var ChangeDelay : Real = 0.5;
    
    var CharWorldPos : Real3 = Real3();
    
    var MainSpace : Space = null;
    
    var Timer : Integer = 0;
    var BPressed : Boolean = false;
    var HasWind : Boolean = false;
    var Player : Cog = null;
    var IsUnskippable : Boolean = false;
    
    function Initialize(init : CogInitializer)
    {
        this.Gamepad = Zero.Gamepads.GetGamePad(this.GamepadIndex);
        this.TextBox = this.TextBoxPath.Cog;
        
        this.TextIsScrolling = true;
        
        this.MainSpace = this.GameSession.FindSpaceByName("Main");
        this.Player = this.MainSpace.FindObjectByName("Player");
        
        this.HasWind = this.Player.PlayerController.WindActive;
        
        
        
        this.CharWorldPos = this.Owner.SpriteText.GetCharacterPosition(this.CurrentIndex - 1);
        this.Particle = this.Space.FindObjectByName("TextBoxParticle");
        this.ParticleChild = this.Particle.FindChildByName("TextBoxParticleChild");
        this.Particle.Transform.Translation = this.CharWorldPos + Real3(1, -0.4, 5);
        
        Zero.Connect(this.Owner, "textScrollEvent", this.OntextScrollEvent);
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Space, "dialogueFinished", this.OnDialogueFinished);
        Zero.Connect(this.Space, "CancelDialogue", this.OnCancelDialogue);
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        this.IsUnskippable = this.Space.FindObjectByName("TextBox").TextBoxManager.Unskippable;
        
        if(this.DialogueSequence == true)
        {
            var textManager = this.Space.FindObjectByName("TextBox").TextBoxManager;
            var diagSeg = textManager.DialogueSegment;
            
            if(textManager.DialogueFinished == true)
            {
                this.Owner.SpriteText.Text = "";
                //var mainSpace = this.GameSession.FindSpaceByName("Main");
                //var dialogueFinished = new DialogueEventFinished();
                //dialogueFinished.CurrentDialogueSegment = diagSeg;
                //mainSpace.DispatchEvent("dialogueFinished", dialogueFinished);
                textManager.DialogueFinished = false;
            }
            
            if(this.Gamepad.IsButtonPressed(Buttons.A) || Zero.Keyboard.KeyIsPressed(Keys.Space))
            {
                //Change this to a different button.
                if(this.TextIsScrolling == false)
                {
                    //this.TextSeq.Cancel();
                    this.ChangeSeq = Action.Sequence(this.Owner.Actions);
                    Actions.Delay(this.ChangeSeq, this.ChangeDelay);
                    Actions.Call(this.ChangeSeq, this.ContinueEventCall);
                    this.TextIsScrolling = true;
                }
                else
                {
                    this.LetterDelay = 0.0;
                    this.PunctuationDelay = 0.0;
                    this.DialogueSpeedIncreased = true;
                }
            }
            
            if(this.IsUnskippable == false)
            {
                if(this.Gamepad.IsButtonPressed(Buttons.B) && this.MainSpace.LevelSettings.QuickPause.IsPauseActive == false)
                {
                    this.TextIsCancelled = true;
                }
            }
            
            //var localPos = Real3(this.Owner.SpriteText.MeasureGivenText(this.Owner.SpriteText.Text), 0);
            //var worldPos = this.Owner.Transform.TransformPoint(localPos);
            
            this.CharWorldPos = this.Owner.SpriteText.GetCharacterPosition(this.CurrentIndex - 1);
            
            if(textManager.DialogueFinished == false)
            {
                this.Particle.Transform.Translation = Math.Lerp(this.Particle.Transform.Translation,
                this.CharWorldPos + Real3(1, -0.4, 5), 1.0);
            }
        }
    }

    function OntextScrollEvent(event : TextScrollEvent)
    {
        if(this.TextSeq != null)
        {
            this.TextSeq.Cancel();
        }
        //this.PlayerDeactivated = true;
        
        this.Particle.SpriteParticleSystem.Visible = true;
        this.Particle.FindChildByName("TextBoxParticleChild").SpriteParticleSystem.Visible = true;
        
        this.DialogueStartSettings();
        
        this.CurrentIndex = 0;
        this.CurrentString = null;
        this.GoalString = event.TextToWrite.Text;

        this.TextIsScrolling = true;
        
        this.TextSeq = Action.Sequence(this.Owner.Actions);
        
        Actions.Delay(this.TextSeq, this.TextDelay);
        Actions.Call(this.TextSeq, this.TextScroll);
    }
    
    function TextScroll()
    {
        if(this.GoalString == this.CurrentString)
        {
            Console.WriteLine("Text Scroll End Condition Is Met");
            this.PunctuationDelay = 0.1;
            this.LetterDelay = 0.05;
            this.TextIsScrolling = false;
            this.Particle.SpriteParticleSystem.Visible = false;
            this.ParticleChild.SpriteParticleSystem.Visible = false;
            return;
        }
        
        if(Zero.Keyboard.KeyIsPressed(Keys.Space) || this.Gamepad.IsButtonPressed(Buttons.A))
        {
            if(this.DialogueSpeedIncreased == true)
            {
                this.Owner.SpriteText.Text = this.GoalString;
                this.DialogueSpeedIncreased = false;
                this.TextIsScrolling = false;
                this.Particle.SpriteParticleSystem.Visible = false;
                this.ParticleChild.SpriteParticleSystem.Visible = false;
                return;
            }
        }
        
        if(this.TextIsCancelled == true)
        {
            this.DialogueSequence = false;
            this.Particle.SpriteParticleSystem.Visible = false;
            this.ParticleChild.SpriteParticleSystem.Visible = false;
            return;
        }
        
        this.CurrentString = this.GoalString.SubString(0, this.CurrentIndex + 1);
        
        var currentCharacterInt:Integer = this.GoalString.Get(this.CurrentIndex);
        var currentCharacter:String = null;
        
        currentCharacter = String.FromChar(currentCharacterInt);
        
        this.TextDelay = this.LetterDelay;
        
        if(this.IsPunctuation)
        {
            this.TextDelay = this.PunctuationDelay;
            this.IsPunctuation = false;
        }
        if(currentCharacter == "." || currentCharacter == "?" || currentCharacter == "!")
        {
            this.IsPunctuation = true;
        }
        
        this.CurrentIndex += 1;
        
        if(this.TextIsScrolling)
        {
            this.Owner.SpriteText.Text = this.CurrentString;
            
        }
        
        this.TextSeq = Action.Sequence(this.Owner.Actions);
        
        Actions.Delay(this.TextSeq, this.TextDelay);
        
        Actions.Call(this.TextSeq, this.TextScroll);
        //this.ParticleScroll(this.CurrentIndex);
    }
    
    function ContinueEventCall()
    {
        var e = new ContinueDialogueEvent();
        this.TextBox.DispatchEvent("continueDialogue", e);
    }
    
    function OnDialogueFinished(event : DialogueEventFinished)
    {
        Console.WriteLine("Not sure this should happen");
        this.Owner.SpriteText.Text = "";
    }
    
    function OnCancelDialogue(event : CancelDialogueEvent)
    {
        this.GoalString = this.CurrentString;
        var seq = Action.Sequence(this.Owner.Actions);
        Action.Delay(seq, 0.2);
        Action.Call(seq, this.ChangeCancelDialogueBool);
        this.DialogueSequence = false;
    }
    
    function ChangeCancelDialogueBool()
    {
        Console.WriteLine("!!!!!!!!!!!!!!!!!!!!!!!");
        this.TextIsCancelled = false;
    }
    
    function DialogueStartSettings()
    {
        this.Player.PlayerController.FreezeMovement = true;
        if(this.HasWind == true)
        {
            this.Player.PlayerController.WindActive = false;
        }
        
    }
    
    function DialogueEndSettings()
    {
        this.MainSpace.FindObjectByName("Player").PlayerController.FreezeMovement = false;
        if(this.HasWind == true)
        {
            this.Player.PlayerController.WindActive = true;
        }
    }
}